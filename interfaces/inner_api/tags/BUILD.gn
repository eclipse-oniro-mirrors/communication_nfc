# Copyright (C) 2022 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/config/components/idl_tool/idl.gni")
import("//build/ohos.gni")
import("//build/ohos_var.gni")
import("../../../nfc.gni")

idl_gen_interface("nfc_tag_interface") {
  sources = [ "idl/ITagSession.idl" ]
  configs = [ ":nfc_inner_kits_public_config" ]

  sequenceable_ext_deps = [ "ability_base:want" ]
  log_domainid = "0xD000301"
  log_tag = "Nfc_Tag_Session"
  part_name = "nfc"
  subsystem_name = "communication"
}

config("nfc_inner_kits_config") {
  visibility = [ ":*" ]
  defines = [ "DEBUG" ]
  cflags_cc = [ "-fexceptions" ]

  include_dirs = []
}

config("nfc_inner_kits_public_config") {
  visibility = [ ":*" ]

  include_dirs = [
    "${target_gen_dir}",
    "$NFC_DIR/interfaces/inner_api/common",
    "$NFC_DIR/interfaces/inner_api/controller",
    "$NFC_DIR/interfaces/inner_api/tags",
  ]
}

ohos_shared_library("nfc_inner_kits_tags") {
  sanitize = {
    cfi = true
    boundary_sanitize = true
    integer_overflow = true
    cfi_cross_dso = true
    debug = false
    ubsan = true
  }
  branch_protector_ret = "pac_ret"
  sources = [
    "barcode_tag.cpp",
    "basic_tag_session.cpp",
    "foreground_callback_stub.cpp",
    "iso15693_tag.cpp",
    "isodep_tag.cpp",
    "mifare_classic_tag.cpp",
    "mifare_ultralight_tag.cpp",
    "ndef_formatable_tag.cpp",
    "ndef_tag.cpp",
    "nfca_tag.cpp",
    "nfcb_tag.cpp",
    "nfcf_tag.cpp",
    "reader_mode_callback_stub.cpp",
    "tag_foreground.cpp",
  ]

  configs = [ ":nfc_inner_kits_config" ]

  public_configs = [ ":nfc_inner_kits_public_config" ]

  deps = [
    ":libnfc_tag_interface_proxy",
    ":nfc_tag_interface",
    "../common:nfc_inner_kits_common",
    "../controller:nfc_inner_kits_controller",
  ]

  external_deps = [
    "ability_base:want",
    "ability_base:zuri",
    "ability_runtime:dataobs_manager",
    "bundle_framework:appexecfwk_base",
    "c_utils:utils",
    "hilog:libhilog",
    "ipc:ipc_core",
    "samgr:samgr_proxy",
  ]

  part_name = "nfc"
  subsystem_name = "communication"
}
