# Copyright (C) 2025 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/config/components/ets_frontend/ets2abc_config.gni")
import("//build/ohos.gni")
import("//build/ohos/taihe_idl/taihe.gni")
import("../../../../nfc.gni")

copy_taihe_idl("copy_taihe_nfc_tag_session") {
  sources = [ "idl/tagSession.taihe" ]
}

copy_taihe_idl("copy_taihe_nfc_tag") {
  sources = [
    "idl/nfctech.taihe",
    "idl/ohos.nfc.tag.tag.taihe",
  ]
  deps = [ ":copy_taihe_nfc_tag_session" ]
}

copy_taihe_idl("copy_taihe_nfc_tag_ndef") {
  sources = [ "idl/ohos.nfc.tag.tag.ndef.taihe" ]
  deps = [ ":copy_taihe_nfc_tag" ]
}

subsystem_name = "communication"
part_name = "nfc"
taihe_generated_file_path_nfc_tag = "$taihe_file_path/out/$subsystem_name/$part_name/nfc_tag"

ohos_taihe("run_taihe") {
  taihe_generated_file_path = "${taihe_generated_file_path_nfc_tag}"
  deps = [
    ":copy_taihe_nfc_tag_session",
    ":copy_taihe_nfc_tag",
    ":copy_taihe_nfc_tag_ndef"
  ]
  outputs = [
    "$taihe_generated_file_path/src/nfctech.ani.cpp",
    "$taihe_generated_file_path/src/nfctech.abi.c",
    "$taihe_generated_file_path/src/ohos.nfc.tag.tag.ani.cpp",
    "$taihe_generated_file_path/src/ohos.nfc.tag.tag.abi.c",
    "$taihe_generated_file_path/src/ohos.nfc.tag.tag.ndef.ani.cpp",
    "$taihe_generated_file_path/src/ohos.nfc.tag.tag.ndef.abi.c",
    "$taihe_generated_file_path/src/tagSession.ani.cpp",
    "$taihe_generated_file_path/src/tagSession.abi.c",
  ]
}

taihe_shared_library("nfc_fwk_taihe_tag") {
  sanitize = {
    cfi = true
    boundary_sanitize = true
    integer_overflow = true
    cfi_cross_dso = true
    debug = false
    ubsan = true
  }
  branch_protector_ret = "pac_ret"

  taihe_generated_file_path = "${taihe_generated_file_path_nfc_tag}"
  sources = get_target_outputs(":run_taihe")

  defines = [ "DEBUG" ]
  include_dirs = [
    "$NFC_DIR/interfaces/inner_api/common",
  ]
  sources += [
    "src/ani_constructor.cpp",
    "src/impl/ohos.nfc.tag.tag.impl.cpp",
    "src/impl/ohos.nfc.tag.tag.ndef.impl.cpp",
  ]

  external_deps = [
    "c_utils:utils",
    "hilog:libhilog",
  ]

  deps = [
    ":run_taihe",
    "$NFC_DIR/interfaces/inner_api/common:nfc_inner_kits_common",
    "$NFC_DIR/interfaces/inner_api/tags:nfc_inner_kits_tags"
  ]

  part_name = "$part_name"
  subsystem_name = "$subsystem_name"
}

generate_static_abc("nfc_fwk_etc_tag_abc") {
  base_url = "${taihe_generated_file_path_nfc_tag}"
  files = [
    "${taihe_generated_file_path_nfc_tag}/@ohos.nfc.tag.ets",
    "${taihe_generated_file_path_nfc_tag}/nfctech.ets",
    "${taihe_generated_file_path_nfc_tag}/tagSession.ets",
  ]
  is_boot_abc = "True"
  device_dst_file = "/system/framework/nfc_fwk_etc_tag_abc.abc"
  dependencies = [ ":run_taihe" ]
}

ohos_prebuilt_etc("nfc_fwk_etc_tag") {
  source = "$target_out_dir/nfc_fwk_etc_tag_abc.abc"
  deps = [ ":nfc_fwk_etc_tag_abc" ]
  module_install_dir = "framework"
  part_name = "$part_name"
  subsystem_name = "$subsystem_name"
}

group("nfc_taihe_tag") {
  deps = [
    ":run_taihe",
    ":nfc_fwk_etc_tag",
    ":nfc_fwk_taihe_tag",
  ]
}