# Copyright (C) 2025 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/config/components/ets_frontend/ets2abc_config.gni")
import("//build/ohos.gni")
import("//build/ohos/taihe_idl/taihe.gni")
import("../../../../nfc.gni")

copy_taihe_idl("copy_taihe_nfc_cardEmulation") {
  sources = [ "idl/ohos.nfc.cardEmulation.nfcCardEmulation.taihe" ]
}

subsystem_name = "communication"
part_name = "nfc"
taihe_generated_file_path_nfcCardEmulation = "$taihe_file_path/out/$subsystem_name/$part_name/nfcCardEmulation"

ohos_taihe("run_taihe") {
  taihe_generated_file_path = "${taihe_generated_file_path_nfcCardEmulation}"
  deps = [ ":copy_taihe_nfc_cardEmulation" ]
  outputs = [
    "$taihe_generated_file_path/src/ohos.nfc.cardEmulation.nfcCardEmulation.ani.cpp",
    "$taihe_generated_file_path/src/ohos.nfc.cardEmulation.nfcCardEmulation.abi.c",
  ]
}

taihe_shared_library("nfc_fwk_taihe_cardEmulation") {
  sanitize = {
    cfi = true
    boundary_sanitize = true
    integer_overflow = true
    cfi_cross_dso = true
    debug = false
    ubsan = true
  }
  branch_protector_ret = "pac_ret"

  taihe_generated_file_path = "${taihe_generated_file_path_nfcCardEmulation}"
  sources = get_target_outputs(":run_taihe")

  defines = [ "NFC_DEBUG" ]
  include_dirs = [
    "inc",
    "$NFC_DIR/frameworks/ets/taihe/nfc_common",
    "$NFC_DIR/interfaces/inner_api/common",
  ]
  sources += [
    "src/ani_constructor.cpp",
    "src/nfc_taihe_card_emulation_event.cpp",
    "src/impl/ohos.nfc.cardEmulation.nfcCardEmulation.impl.cpp"
  ]

  external_deps = [
    "ability_base:want",
    "bundle_framework:appexecfwk_base",
    "bundle_framework:appexecfwk_core",
    "bundle_framework:bms_ani_common",
    "c_utils:utils",
    "hilog:libhilog",
    "ipc:ipc_core",
    "samgr:samgr_proxy",
  ]

  deps = [
    ":run_taihe",
    "$NFC_DIR/interfaces/inner_api/common:nfc_inner_kits_common",
    "$NFC_DIR/interfaces/inner_api/cardEmulation:nfc_inner_kits_card_emulation"
  ]

  part_name = "$part_name"
  subsystem_name = "$subsystem_name"
}

generate_static_abc("nfc_fwk_etc_cardEmulation_abc") {
  base_url = "${taihe_generated_file_path_nfcCardEmulation}"
  files = [ "${taihe_generated_file_path_nfcCardEmulation}/@ohos.nfc.cardEmulation.ets" ]
  is_boot_abc = "True"
  device_dst_file = "/system/framework/nfc_fwk_etc_cardEmulation_abc.abc"
  dependencies = [ ":run_taihe" ]
}

ohos_prebuilt_etc("nfc_fwk_etc_cardEmulation") {
  source = "$target_out_dir/nfc_fwk_etc_cardEmulation_abc.abc"
  deps = [ ":nfc_fwk_etc_cardEmulation_abc" ]
  module_install_dir = "framework"
  part_name = "$part_name"
  subsystem_name = "$subsystem_name"
}

group("nfc_taihe_cardEmulation") {
  deps = [
    ":run_taihe",
    ":nfc_fwk_etc_cardEmulation",
    ":nfc_fwk_taihe_cardEmulation",
  ]
}